plugins {
    id 'com.android.application'
}
import com.android.build.api.variant.ScopedArtifacts
import com.android.build.api.artifact.ScopedArtifact

import org.gradle.api.DefaultTask;
import org.gradle.api.tasks.TaskAction;
import javassist.ClassPool;
import javassist.CtClass;

abstract class AddClassesTask extends DefaultTask {

    @OutputFiles
    abstract DirectoryProperty getOutput();

    @TaskAction
    void taskAction() {

        ClassPool pool = new ClassPool(ClassPool.getDefault());
        CtClass interfaceClass = pool.makeInterface("com.android.api.tests.SomeInterface");
        System.out.println("Adding $interfaceClass");
        interfaceClass.writeFile(output.get().asFile.absolutePath);
    }
}

android {
    namespace = "com.android.build.example.minimal"
    compileSdkVersion(29)
    defaultConfig {
        minSdkVersion(21)
    }
}

androidComponents {
    onVariants(selector().all(), { variant ->
        TaskProvider<AddClassesTask> taskProvider = project.tasks.register(variant.getName() + "AddAllClasses", AddClassesTask.class)
        variant.artifacts
            .forScope(ScopedArtifacts.Scope.PROJECT)
            .use(taskProvider)
            .toAppend(
                ScopedArtifact.CLASSES.INSTANCE,
                { it.getOutput() }
            )
    })
}