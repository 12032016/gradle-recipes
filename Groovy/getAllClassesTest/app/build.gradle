plugins {
    id 'com.android.application'
}


import org.gradle.api.DefaultTask
import org.gradle.api.file.Directory
import org.gradle.api.file.RegularFile
import org.gradle.api.tasks.InputFiles
import org.gradle.api.tasks.TaskAction
import org.gradle.api.provider.ListProperty

abstract class GetAllClassesTask extends DefaultTask {

    @InputFiles
    abstract ListProperty<Directory> getAllDirectories()

    @InputFiles
    abstract ListProperty<RegularFile> getAllJars()

    @TaskAction
    void taskAction() {

        allDirectories.get().forEach { directory ->
            println("Directory : ${directory.asFile.absolutePath}")
            directory.asFile.traverse(type: groovy.io.FileType.FILES) { file ->
                println("File : ${file.absolutePath}")
            }
        }
        allJars.get().forEach { file ->
            println("JarFile : ${file.asFile.absolutePath}")
        }
    }
}


android {
    namespace = "com.android.build.example.minimal"
    compileSdkVersion(29)
    defaultConfig {
        minSdkVersion(21)
    }
}

import com.android.build.api.variant.ScopedArtifacts
import com.android.build.api.artifact.ScopedArtifact
androidComponents {
    onVariants(selector().all(), { variant ->
        TaskProvider taskProvider = project.tasks.register(variant.getName() + "GetAllClasses", GetAllClassesTask.class)
        variant.artifacts.forScope(ScopedArtifacts.Scope.PROJECT)
            .use(taskProvider)
            .toGet(
                ScopedArtifact.CLASSES.INSTANCE,
                { it.allJars },
                { it.allDirectories }
            )
    })
}